import time
import os
from shutil import copy2
import pathlib
import pandas as pd

configfile: "config/config.yaml"
SAMPLES = config['SAMPLES']

OUTDIR = config['parameters']['outdir'] + "/"

onstart:
    print("This is MABA16S")
    time.sleep(1)

    # copy the config file to output dir
    pathlib.Path(OUTDIR).mkdir(parents=True, exist_ok=True)
    copy2('config/config.yaml', OUTDIR)

    for i in SAMPLES.items():
        print(i[0], '\t', i[1])
    print(f'output directory is: {OUTDIR}')

onerror:
    print(f"these were the samples: {SAMPLES}")
    print("error has occured")


localrules: all, download_kraken2_db, combinereads

rule all:
    input:
        expand(OUTDIR + "kraken2/{sample}/krakenreport.txt", sample=SAMPLES),
        expand(OUTDIR + "kraken2/{sample}/krakenreport_filtered.txt", sample=SAMPLES),
        expand(OUTDIR + "kraken2/{sample}/genus", sample=SAMPLES),
        expand(OUTDIR + 'kraken2consensus/{sample}/', sample=SAMPLES)


rule download_kraken2_db:
    output:
        directory("db/silva")
    threads: 1
    conda:
        "envs/kraken2.yaml"
    shell:
        "kraken2-build --db {output} --special silva"


rule combinereads:
    input:
        lambda wildcards: SAMPLES[wildcards.sample]
    output:
        temp(OUTDIR + "reads/{sample}.fastq.gz")
    threads: 1
    shell:
        "cat {input}/*fastq.gz > {output}"


rule kraken2:
    input:
        reads = rules.combinereads.output,
        db = rules.download_kraken2_db.output
    output:
        report = OUTDIR + "kraken2/{sample}/krakenreport.txt",
        out = OUTDIR + "kraken2/{sample}/output.txt"
    threads: 4
    conda:
        "envs/kraken2.yaml"
    log:
        OUTDIR + "log/kraken2/{sample}.txt"
    shell:
        "kraken2 --db {input.db} -t {threads} "
        "--report {output.report} {input.reads} > {output.out} "


rule strip_genera:
    input:
        rules.kraken2.output.report
    output:
        OUTDIR + "kraken2/{sample}/krakenreport_filtered.txt"
    params:
        reads_cutoff_genus = 50
    shell:
        '''awk '$4 == "G" {{print $0}}' {input} | awk '$2 > {params.reads_cutoff_genus} {{print $0}}' > {output}'''


def extract_reads(krakenfile, outputdir):
    with open(krakenfile) as file:
        for line in file:
            print(line)


rule genus_read_extract:
    input:
        report = OUTDIR + "kraken2/{sample}/krakenreport_filtered.txt",
        krakenfile = rules.kraken2.output.out,
        fastq = rules.combinereads.output
    output:
        directory(OUTDIR + "kraken2/{sample}/genus")
    threads:
        1
    conda:
        "envs/kraken2.yaml"
    log:
        OUTDIR + "log/kraken2/{sample}/genusreadextract.log"
    shell:
        '''
        python scripts/krakenextract.py {input.report} {input.krakenfile} {input.fastq} {output}
        '''


rule map_genera:
    input:
        readdir = rules.genus_read_extract.output,
        ref = "db/silva/data/SILVA_138.1_SSURef_NR99_tax_silva.fasta"
    output:
        directory(OUTDIR + 'kraken2consensus/{sample}/')
    threads: 1
    conda:
        "envs/minimapsamtools.yaml"
    log:
        OUTDIR + "log/map_genera/{sample}.log"
    shell:
        "python scripts/aligner.py {input.readdir} {input.ref} {output} 2> {log}"
