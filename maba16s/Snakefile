import time
import os
from shutil import copy2
import pathlib

configfile: "config/config.yaml"
SAMPLES = config['SAMPLES']

OUTDIR = config['parameters']['outdir'] + "/"
print(SAMPLES)

onstart:
    print("This is MABA16S")
    time.sleep(1)

    # copy the config file to output dir
    pathlib.Path(OUTDIR).mkdir(parents=True, exist_ok=True)
    copy2('config/config.yaml', OUTDIR )


    for i in SAMPLES.items():
        print(i[0], '\t', i[1])
    print(f'output directory is: {OUTDIR}')

onerror:
    print(f"these were the samples: {SAMPLES}")
    print("error has occured")


rule all: 
    input:
        expand(OUTDIR + "{sample}/krakenreport.txt", sample = SAMPLES)
 

rule download_kraken2_db:
    output:
        directory("db/silva")
    threads: 1
    conda:
        "envs/kraken2.yaml"
    shell:
        "kraken2-build --db {output} --special silva"



rule combinereads:
    input:
        lambda wildcards: SAMPLES[wildcards.sample]
    output:
        OUTDIR + "reads/{sample}.fastq.gz"
    threads: 1 
    shell:
        "cat {input}/*fastq.gz > {output}"


rule kraken2:
    input: 
        reads = rules.combinereads.output,
        db = rules.download_kraken2_db.output
    output:
        report = OUTDIR + "{sample}/krakenreport.txt",
        out = OUTDIR + "{sample}/output.txt"
    threads: 4
    conda:
        "envs/kraken2.yaml"
    log:
        OUTDIR + "log/kraken2/{sample}.txt"
    shell:
        """
        kraken2 --db {input.db} -t {threads}  --report {output.report} \ 
        {input.reads} > {output.out} 
        """ 

